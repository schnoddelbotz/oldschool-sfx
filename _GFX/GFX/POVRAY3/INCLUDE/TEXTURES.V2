#declare Textures_Inc_Temp = version
#version 3.0
#ifdef(View_POV_Include_Stack)
#   debug "including texture.v2\n"
#end
/*

              Persistence of Vision Raytracer Version 3.0
                     Standard textures include file

    Contents:
    ===========================
    DMFWood6
    NBglass            (Norm Bowler)
    NBoldglass         (Norm Bowler)
    NBwinebottle       (Norm Bowler)
    NBbeerbottle       (Norm Bowler)
    Ruby_Glass         (after Norm Bowler)
    Dark_Green_Glass   (after Norm Bowler)
    Yellow_Glass       (after Norm Bowler)
    Orange_Glass       (after Norm Bowler)
    Vicks_Bottle_Glass (after Norm Bowler)
    Soft_Silver        (Dan Farmer)
    New_Penny          (Dan Farmer)
    Tinny_Brass        (Dan Farmer)
    Gold_Nugget        (Dan Farmer)
    Aluminum           (Dan Farmer)
    Bright_Bronze      (Dan Farmer)
    Lightening1        (Dan Farmer)
    Lightening2        (Dan Farmer)
    Brushed_Aluminum   (Dan Farmer)
    Starfield          (Jeff Burton )
    Shadow_Clouds      (Bill Pulver)

*/


//*****************************************************************************
//                      STONE PIGMENTS
//*****************************************************************************

#declare Jade_Map =
color_map {
    [0.0, 0.8 color rgb <0.1, 0.6, 0.1>
              color rgb <0.0, 0.3, 0.0>]
    [0.8, 1.0 color rgb <0.1, 0.6, 0.1>
              color rgb <0.0, 0.2, 0.0>]
}

// Drew Wells' superb Jade.  Color map works nicely with other textures, too.
#declare Jade = 
pigment {
    marble
    turbulence 1.8
    color_map { Jade_Map }
}

#declare Red_Marble_Map = 
color_map {
    [0.0, 0.8 color rgb <0.8, 0.8, 0.6>
              color rgb <0.8, 0.4, 0.4>]
    [0.8, 1.0 color rgb <0.8, 0.4, 0.4>
              color rgb<0.8, 0.2, 0.2>]
}
// Classic white marble with red veins.  Over-worked, like checkers.
#declare Red_Marble = 
pigment {
    marble
    color_map { Red_Marble_Map }
    turbulence 1
}

#declare White_Marble_Map =
color_map {
    [0.0, 0.8 color rgb <0.9, 0.9, 0.9>
              color rgb <0.5, 0.5, 0.5>]
    [0.8, 1.0 color rgb <0.5, 0.5, 0.5>
              color rgb <0.2, 0.2, 0.2>]
}

// White marble with black veins.
#declare White_Marble = 
pigment {
    marble
    turbulence 1
    color_map { White_Marble_Map }
}

// Light blue and black marble with a thin red vein 
// Try changing LBlue and Vein below to modify the marble.
#declare LBlue = color rgb <0.0, 0.6, 0.6>
#declare Vein  = color rgb <0.6, 0.0, 0.0>

#declare Blood_Marble_Map =
color_map {
    [0.0, 0.8 color Black  color LBlue]
    [0.8, 0.9 color LBlue  color Vein]
    [0.9, 1.0 color Vein   color Black]
}
#declare Blood_Marble = 
pigment {
    marble
    color_map { Blood_Marble_Map }
    turbulence 2.3
}

#declare Blue_Agate_Map =
color_map {
    [0.0, 0.5  color rgb <0.30, 0.30, 0.50>
               color rgb <0.30, 0.30, 0.50>]
    [0.5, 0.55 color rgb <0.30, 0.30, 0.50>
               color rgb <0.20, 0.20, 0.30>]
    [0.55, 0.6 color rgb <0.20, 0.20, 0.30>
               color rgb <0.25, 0.25, 0.35>]
    [0.6, 0.7  color rgb <0.25, 0.25, 0.35>
               color rgb <0.15, 0.15, 0.26>]
    [0.7, 0.8  color rgb <0.15, 0.15, 0.26>
               color rgb <0.10, 0.10, 0.20>]
    [0.8, 0.9  color rgb <0.10, 0.10, 0.20>
               color rgb <0.30, 0.30, 0.50>]
    [0.9, 1.0  color rgb <0.30, 0.30, 0.50>
               color rgb <0.10, 0.10, 0.20>]
}

// a grey blue agate -- kind of purplish.
#declare Blue_Agate = 
pigment {
    agate
    color_map { Blue_Agate_Map }
}

#declare Sapphire_Agate_Map = 
color_map {
    [0.0, 0.3 color rgb <0.0, 0.0, 0.9>
              color rgb <0.0, 0.0, 0.8>]
    [0.3, 1.0 color rgb <0.0, 0.0, 0.8>
              color rgb <0.0, 0.0, 0.4>]
}

// Deep blue agate -- almost glows.
#declare Sapphire_Agate = 
pigment { 
    agate
    color_map { Sapphire_Agate_Map }
}

#declare Brown_Agate_Map =
color_map {
    [0.0, 0.5 color rgb <0.0, 0.0, 0.0>
              color rgb <0.9, 0.7, 0.6>]
    [0.5, 0.6 color rgb <0.9, 0.7, 0.6>
              color rgb <0.9, 0.7, 0.4>]
    [0.6, 1.0 color rgb <0.9, 0.7, 0.4>
              color rgb <0.7, 0.4, 0.2>]
}

// Brown and white agate -- very pretty.
#declare Brown_Agate = 
pigment {
    agate
    color_map { Brown_Agate_Map }
}

#declare Pink_Granite_Map =
color_map {
    [0.0,  0.4  color rgb <0.0, 0.0, 0.0>  
                color rgb <0.0, 0.0, 0.0>]
    [0.4,  0.45 color rgb <0.85, 0.85, 0.95> 
                color rgb <0.85, 0.85, 0.95>]
    [0.45, 0.5  color rgb <0.85, 0.85, 0.95> 
                color rgb <0.75, 0.75, 0.75>]
    [0.5,  0.55 color rgb <0.75, 0.75, 0.75>   
                color rgb <0.82, 0.57, 0.46>]
    [0.55, 0.8  color rgb <0.82, 0.57, 0.46> 
                color rgb <0.82, 0.57, 0.46>]
    [0.8,  1.0  color rgb <0.82, 0.57, 0.46> 
                color rgb <1.0, 0.5, 0.0> ]
}

#declare Pink_Granite =  
pigment {
    granite
    color_map { Pink_Granite_Map }
}

//*****************************************************************************
//                      STONE TEXTURES
//*****************************************************************************

// Gray-pink alabaster or marble.  Layers are scaled for a unit object
// and relative to each other.
#declare PinkAlabaster =
// Underlying surface is very subtly mottled with bozo
texture {
    pigment {
        bozo
        turbulence 0.25
        color_map {
            [0, 1 color rgb <0.9, 0.75, 0.75>
                  color rgb <0.6, 0.6, 0.6> ]
        }
    scale 0.4
    }
    finish{ ambient 0.25 }
}

// Second layer texture has some filter values, yet a fair amount of color
// Viening is kept quite thin in color map and by the largish scale.
texture {
    pigment {
        granite
        color_map {
            [0.0, 0.9 color rgbf <0.52, 0.39, 0.39, 1.0> 
                      color rgbf <0.52, 0.39, 0.39, 0.5>]
            [0.9, 1.0 color rgbf <0.42, 0.14, 0.55, 0.0>
                      color rgbf <0.42, 0.14, 0.55, 0.0>]
        }
    scale 2  
    }
    finish {
        specular 1   
        roughness 0.0001
        phong 0.25
        phong_size 75
        brilliance 4
    }
}


//*****************************************************************************
//                           SKY PIGMENTS
//*****************************************************************************
#declare Blue_Sky_Map =
color_map {
    [0.0, 0.5 color rgb <0.25, 0.25, 0.5>
              color rgb <0.25, 0.25, 0.5>]
    [0.5, 0.6 color rgb <0.25, 0.25, 0.5>
              color rgb <0.7, 0.7, 0.7>]
    [0.6, 1.0 color rgb <0.7, 0.7, 0.7>
              color rgb <0.3, 0.3, 0.3>]
}

// Basic Blue Sky w/ clouds.
#declare Blue_Sky = 
pigment {
    bozo
    color_map { Blue_Sky_Map }
    turbulence 0.3
}

// Bright Blue Sky w/ very white clouds.
#declare Bright_Blue_Sky = 
pigment {
    bozo
    turbulence 0.56
    color_map {
        [0.0, 0.5 color rgb <0.5, 0.5, 1.0>
                  color rgb <0.5, 0.5, 1.0>]
        [0.5, 0.6 color rgb <0.5, 0.5, 1.0>
                  color rgb <1.0, 1.0, 1.0>]
        [0.6, 1.0 color rgb <1.0, 1.0, 1.0>
                  color rgb <0.5, 0.5, 0.5>]
    }
}

// Another sky
#declare Blue_Sky2 =
pigment {
    agate
    color_map {
        [0.0, 0.3 color rgb <0.0, 0.0, 1.0> 
                  color rgb <0.0, 0.0, 1.0>]
        [0.3, 1.0 color rgb <0.0, 0.0, 1.0> 
                  color rgb <1.0, 1.0, 1.0>]
    }
    scale .75
}

// Small puffs of white clouds
#declare Blue_Sky3 = 
pigment {
    granite
    turbulence 0.1
    color_map {
        [0.0, 0.3 color rgb <0.0, 0.0, 1.0> 
                  color rgb <0.0, 0.0, 1.0>]
        [0.3, 1.0 color rgb <0.0, 0.0, 1.0> 
                  color rgb <1.0, 1.0, 1.0>]
    }
    scale .75
}

// Red sky w/ yellow clouds -- very surreal.
#declare Blood_Sky = 
pigment {
    bozo
    turbulence 0.5
    color_map {
        [0.0, 0.5 color rgb <0.9, 0.700, 0.0>
                  color rgb <0.3, 0.200, 0.0>]
        [0.5, 0.6 color rgb <0.6, 0.025, 0.0>
                  color rgb <0.9, 0.700, 0.0>]
        [0.6, 1.0 color  rgb <0.6, 0.025, 0.0>
                  color rgb <0.6, 0.025, 0.0>]
    }
}

// Black sky with red and purple clouds 
// Try adding turbulence values from 0.1 - 5.0 -- CdW
#declare Apocalypse = 
pigment {
    bozo
    color_map {
        [0.0, 0.4 color rgb <0.8, 0.0, 0.0>
                  color rgb <0.4, 0.0, 0.4>]
        [0.4, 0.6 color rgb <0.4, 0.0, 0.4>
                  color rgb <0.0, 0.0, 0.2>]
        [0.6, 1.0 color rgb <0.0, 0.0, 0.2>
                  color rgb <0.0, 0.0, 0.0>]
    }
}

// White clouds w/ transparent sky.
#declare Clouds = 
pigment {
    bozo
    turbulence 0
    color_map {
        [0.0, 0.1 color rgbf <0.8, 0.8, 0.8, 0.0>
                  color rgbf <0.8, 0.8, 0.8, 0.0>]
        [0.1, 0.5 color rgbf <0.8, 0.8, 0.8, 0.0>
                  color rgbf <1.0, 1.0, 1.0, 1.0>] 
        [0.5, 1.0 color rgbf <1.0, 1.0, 1.0, 1.0>
                  color rgbf <1.0, 1.0, 1.0, 1.0>]
    }
}

#declare FBM_Clouds =
pigment {
    bozo
    turbulence 0.65
    octaves 6
    omega 0.7
    lambda 2
    color_map { 
        [0.0, 0.1   color red 0.85 green 0.85 blue 0.85 
                    color red 0.75 green 0.75 blue 0.75] 
        [0.1, 0.5   color red 0.75 green 0.75 blue 0.75 
                    color Clear] 
        [0.5, 1.001 color Clear color Clear] 
    } 
scale <6, 1, 6>
}

#declare Shadow_Clouds =
texture {                          // The blue sky background for the clouds 
    pigment { color SkyBlue }
    finish { ambient 0.7 diffuse 0 }
}
texture {                          // The upper part of the clouds
    pigment { FBM_Clouds }
    finish { ambient 1.0 diffuse 0 }
}
texture {                          // The darker underside of the clouds
    pigment { FBM_Clouds  translate -0.15*y }
    finish { ambient 0.6 diffuse 0 }
}



//*****************************************************************************
//                           WOODEN PIGMENTS
//*****************************************************************************

// Several wooden pigments by Tom Price:
// A light reddish wood.
#declare Cherry_Wood =
pigment {
    wood
    turbulence 0.3
    color_map {
        [0.0, 0.8 color rgb <0.66, 0.31, 0.20>
                  color rgb <0.66, 0.31, 0.20>]
        [0.8, 1.0 color rgb <0.40, 0.13, 0.06>
                  color rgb <0.20, 0.06, 0.03>]
    }
}

// A light tan wood with,ish rings.
#declare Pine_Wood = 
pigment {
    wood
    turbulence 0.2
    color_map {
        [0.0, 0.8 color rgb <1.0, 0.72, 0.25>
                  color rgb <1.0, 0.72, 0.25>]
        [0.8, 1.0 color rgb <0.5, 0.50, 0.06>
                  color rgb <0.4, 0.40, 0.03>]
    }
}

// Dark wood with a,ish hue to it.
#declare Dark_Wood = 
pigment {
    wood
    turbulence 0.2
    color_map {
        [0.0, 0.8 color rgb <0.43, 0.24, 0.05>
                  color rgb <0.43, 0.24, 0.05>]
        [0.8, 1.0 color rgb <0.40, 0.33, 0.06>
                  color rgb <0.20, 0.03, 0.03>]
    }
}

// Light tan wood with brown rings.
#declare Tan_Wood = 
pigment {
    wood
    turbulence 0.1
    color_map {
        [0.0, 0.8 color rgb <0.88, 0.60, 0.30>
                  color rgb <0.88, 0.60, 0.30>]
        [0.8, 1.0 color rgb <0.60, 0.40, 0.20>
                  color rgb <0.40, 0.30, 0.20>]
    }
}

// A very pale wood with tan rings -- kind of balsa-ish.
#declare White_Wood = 
pigment {
    wood
    turbulence 0.6
    color_map {
        [0.0, 0.8 color rgb <0.93, 0.71, 0.53>
                  color rgb <0.98, 0.81, 0.60>]
        [0.8, 1.0 color rgb <0.60, 0.33, 0.27>
                  color rgb <0.70, 0.60, 0.23>]
    }
}

// Brown wood - looks stained.
#declare Tom_Wood = 
pigment {
    wood
    turbulence 0.31
    color_map {
        [0.0, 0.8 color rgb < 0.7, 0.3, 0.0>
                  color rgb < 0.7, 0.3, 0.0>]
        [0.8, 1.0 color rgb < 0.5, 0.2, 0.0>
                  color rgb < 0.4, 0.1, 0.0>]
    }
}

// The scaling in these definitions is relative to a unit-sized object
// (radius 1).  Note that woods are functionally equivilent to a log lying
// along the z axis.  For best results, think like a woodcutter trying to
// extract the nicest board out of that log.  A little tilt along the x axis
// will give elliptical rings of grain like you'd expect to find on most
// boards.  Experiment.
// (The first five came from DODEC2.POV in the POV Scenefile Library.)
#declare DMFWood1 = 
pigment {
    wood
    turbulence 0.04
    octaves 3
    scale <0.05, .05, 1>
    color_map {
        [0.0, 0.1 color rgb <0.60, 0.30, 0.18>
                  color rgb <0.60, 0.30, 0.18>]
        [0.1, 0.9 color rgb <0.60, 0.30, 0.18>
                  color rgb <0.30, 0.15, 0.09>]
        [0.9, 1.0 color rgb <0.30, 0.15, 0.09>
                  color rgb <0.30, 0.15, 0.09>]
    }
}

#declare DMFWood2 = 
pigment {
    wood
    turbulence 0.03
    octaves 4
    scale <0.05, .05, 1>
    color_map {
        [0.0, 0.1 color rgb <0.52, 0.37, 0.26>
                  color rgb <0.52, 0.37, 0.26>]
        [0.1, 0.9 color rgb <0.52, 0.37, 0.26>
                  color rgb <0.42, 0.26, 0.15>]
        [0.9, 1.0 color rgb <0.42, 0.26, 0.15>
                  color rgb <0.42, 0.26, 0.15>]
    }
}

#declare DMFWood3 = 
pigment {
    wood
    turbulence 0.05
    octaves 2
    scale <0.05, .05, 1>
    color_map {
        [0.0, 0.1 color rgb <0.4, 0.133, 0.066>
                  color rgb <0.4, 0.133, 0.066>]
        [0.1, 0.9 color rgb <0.4, 0.133, 0.066>
                  color rgb <0.2, 0.065, 0.033>]
        [0.9, 1.0 color rgb <0.2, 0.065, 0.033>
                  color rgb <0.2, 0.065, 0.033>]
    }
}

#declare DMFWood4 =
pigment {
    wood
    turbulence 0.04
    octaves 3
    scale <0.05, .05, 1>
    color_map {
        [0.0, 0.1 color rgb <0.888, 0.600, 0.3>
                  color rgb <0.888, 0.600, 0.3>]
        [0.1, 0.9 color rgb <0.888, 0.600, 0.3>
                  color rgb <0.600, 0.400, 0.2>]
        [0.9, 1.0 color rgb <0.600, 0.400, 0.2>
                  color rgb <0.600, 0.400, 0.2>]
    }
}

#declare DMFWood5 = 
pigment {
    wood
    turbulence 0.05
    octaves 6
    scale <0.075, .075, 1>
    color_map {
        [0.0, 0.1 color rgb <0.30, 0.10, 0.050>
                  color rgb <0.30, 0.10, 0.050>]
        [0.1, 0.9 color rgb <0.30, 0.10, 0.050>
                  color rgb <0.25, 0.07, 0.038>]
        [0.9, 1.0 color rgb <0.25, 0.07, 0.038>
                  color rgb <0.25, 0.07, 0.038>]
    }
}

// This is a three-layer wood texture.  Renders rather slowly because of
// the transparent layers and the two layers of turbulence, but it looks
// great.  Try other colors of "varnish" for simple variations.  
#declare DMFWood6 = 
texture {
    pigment {
        wood  turbulence 0.04
        octaves 3
        scale <0.05, .05, 1>
        color_map { 
            [0.0, 0.1 color rgb <0.88, 0.60, 0.4>
                      color rgb <0.88, 0.60, 0.4>]
            [0.1, 0.9 color rgb <0.88, 0.60, 0.4>
                      color rgb <0.60, 0.40, 0.3>]
            [0.9, 1.0 color rgb <0.60, 0.40, 0.3>
                      color rgb <0.60, 0.40, 0.3>]
        }
    }
    finish { 
        specular 0.25
        roughness 0.05
        ambient 0.45 
        diffuse 0.33
        reflection 0.15
    }
}
texture {
    pigment {
        wood  turbulence <0.1, 0.5, 1> 
        octaves 5
        lambda 3.25
        scale <0.15, .5, 1>
        color_map { 
            [0.0, 0.1 color rgbf <0.7, 0.6, 0.4, 0.100> 
                      color rgbf <0.8, 0.6, 0.3, 0.500>]
            [0.1, 0.9 color rgbf <0.8, 0.6, 0.3, 0.650>
                      color rgbf <0.6, 0.4, 0.2, 0.975>]
            [0.9, 1.0 color rgbf <0.6, 0.4, 0.2, 0.975>
                      color rgbf <0.6, 0.4, 0.2, 1.000>]
        }
    rotate <5, 10, 5>
    translate -x*2
    }
    finish { 
        specular 0.25 
        roughness 0.0005
        ambient .1 
        diffuse 0.33
    } 
}
// A "coat of varnish" to modify the overall color of the wood
texture {
    pigment { rgbf <0.75, 0.15, 0.0, 0.95> }
    finish { 
        specular 0.25
        roughness 0.01
        ambient 0
        diffuse 0.33
    }
}




// Is this really oak?  I dunno.  Quite light, maybe more like spruce.
#declare DMFLightOak =  
pigment {
    wood
    turbulence 0.05            // For best results,  keep this low!
    scale <0.2, 0.2, 1>        // Scaled for a unit object
    color_map {
        [0.0, 0.1 color rgb <0.42, 0.26, 0.15>
                  color rgb <0.42, 0.26, 0.15>]
        [0.1, 0.9 color rgb <0.42, 0.26, 0.15>
                  color rgb <0.52, 0.37, 0.26>]
        [0.9, 1.0 color rgb <0.52, 0.37, 0.26>
                  color rgb <0.52, 0.37, 0.26>]
    }
}

// Looks like old desk oak if used correctly.
#declare DMFDarkOak = 
pigment {
    wood
    turbulence 0.04            // For best results,  keep this low!
    octaves 3
    scale <0.2, 0.2, 1>        // Scaled for a unit object
    color_map {
        [0.0, 0.1 color rgb <0.60, 0.30, 0.18>
                  color rgb <0.60, 0.30, 0.18>]
        [0.1, 0.9 color rgb <0.60, 0.30, 0.18>
                  color rgb <0.30, 0.15, 0.09>]
        [0.9, 1.0 color rgb <0.30, 0.15, 0.09>
                  color rgb <0.30, 0.15, 0.09>]
    }
}

// Wood by Eric Barish
#declare EMBWood1 = 
texture {  /* Bottom wood-grain layer */
    pigment {
        wood
        turbulence 0.05
        colour_map {
            [0.00 0.34  color rgb <0.58, 0.45, 0.23>
                        color rgb <0.65, 0.45, 0.25>]
            [0.34 0.40  color rgb <0.65, 0.45, 0.25>
                        color rgb <0.33, 0.23, 0.13>]
            [0.40 0.47  color rgb <0.33, 0.23, 0.13>
                        color rgb <0.60, 0.40, 0.20>]
            [0.47 1.00  color rgb <0.60, 0.40, 0.20>
                        color rgb <0.25, 0.15, 0.05>]
        }
    }
    finish {
        crand 0.02
        ambient 0.32
        diffuse 0.63
        phong 0.2
        phong_size 10
    }
    normal { bumps 0.05 }
}
texture {     /* top layer, adds small dark spots */
    pigment {
        bozo
        color_map {
            [0.0 0.8 color rgbf <1.00, 1.00, 1.00, 1.00>
                     color rgbf <1.00, 0.90, 0.80, 0.80>]
            [0.8 1.0 color rgbf <1.00, 0.90, 0.80, 0.80>
                     color rgbf <0.30, 0.20, 0.10, 0.40>]
        }
    scale 0.25
    }
}


//   Doug Otwell woods
//   Yellow pine, close grained
//
#declare Yellow_Pine = 
texture {
    pigment {
        wood
        turbulence 0.02
        color_map {
            [0.000, 0.222 color rgb <0.808, 0.671, 0.251>
                          color rgb <0.808, 0.671, 0.251>]
            [0.222, 0.342 color rgb <0.808, 0.671, 0.251>
                          color rgb <0.600, 0.349, 0.043>]
            [0.342, 0.393 color rgb <0.600, 0.349, 0.043>
                          color rgb <0.808, 0.671, 0.251>]
            [0.393, 0.709 color rgb <0.808, 0.671, 0.251>
                          color rgb <0.808, 0.671, 0.251>]
            [0.709, 0.821 color rgb <0.808, 0.671, 0.251>
                          color rgb <0.533, 0.298, 0.027>]
            [0.821, 1.000 color rgb <0.533, 0.298, 0.027>
                          color rgb <0.808, 0.671, 0.251>]
        }
    scale 0.1
    translate <10, 0, 0>
    }
}
// Yellow_Pine layer 2
texture {
    pigment {
        wood
        turbulence 0.01
        color_map {
            [0.000, 0.120 color rgbf <1.000, 1.000, 1.000, 1.000>
                          color rgbf <0.702, 0.412, 0.118, 0.608>]
            [0.120, 0.231 color rgbf <0.702, 0.412, 0.118, 0.608>
                          color rgbf <0.702, 0.467, 0.118, 0.608>]
            [0.231, 0.496 color rgbf <0.702, 0.467, 0.118, 0.608>
                          color rgbf <1.000, 1.000, 1.000, 1.000>]
            [0.496, 0.701 color rgbf <1.000, 1.000, 1.000, 1.000>
                          color rgbf <1.000, 1.000, 1.000, 1.000>]
            [0.701, 0.829 color rgbf <1.000, 1.000, 1.000, 1.000>
                          color rgbf <0.702, 0.467, 0.118, 0.608>]
            [0.829, 1.000 color rgbf <0.702, 0.467, 0.118, 0.608>
                          color rgbf <1.000, 1.000, 1.000, 1.000>]
        }
    scale 0.5 
    translate <10, 0, 0>
    }
}

//
//   Rosewood
//
#declare Rosewood = 
texture {
    pigment {
        bozo
        turbulence 0.04
        color_map {
            [0.000, 0.256 color rgb <0.204, 0.110, 0.078>
                          color rgb <0.231, 0.125, 0.090>]
            [0.256, 0.393 color rgb <0.231, 0.125, 0.090>
                          color rgb <0.247, 0.133, 0.090>]
            [0.393, 0.581 color rgb <0.247, 0.133, 0.090>
                          color rgb <0.204, 0.110, 0.075>]
            [0.581, 0.726 color rgb <0.204, 0.110, 0.075>
                          color rgb <0.259, 0.122, 0.102>]
            [0.726, 0.983 color rgb <0.259, 0.122, 0.102>
                          color rgb <0.231, 0.125, 0.086>]
            [0.983, 1.000 color rgb <0.231, 0.125, 0.086>
                          color rgb <0.204, 0.110, 0.078>]
        }
    scale <0.5, 0.5, 1>
    translate <10, 0, 0>
    }
    finish {
        ambient 0.5
        diffuse 0.8
    }
}
// Rosewood layer 2
texture {
    pigment {
        wood
        turbulence 0.04
        color_map {
            [0.000, 0.139 color rgbf <0.545, 0.349, 0.247, 1.000>
                          color rgbf <0.000, 0.000, 0.000, 0.004>]
            [0.139, 0.148 color rgbf <0.000, 0.000, 0.000, 0.004>
                          color rgbf <0.000, 0.000, 0.000, 0.004>]
            [0.148, 0.287 color rgbf <0.000, 0.000, 0.000, 0.004>
                          color rgbf <0.545, 0.349, 0.247, 1.000>]
            [0.287, 0.443 color rgbf <0.545, 0.349, 0.247, 1.000>
                          color rgbf <0.545, 0.349, 0.247, 1.000>]
            [0.443, 0.626 color rgbf <0.545, 0.349, 0.247, 1.000>
                          color rgbf <0.000, 0.000, 0.000, 0.004>]
            [0.626, 0.635 color rgbf <0.000, 0.000, 0.000, 0.004>
                          color rgbf <0.000, 0.000, 0.000, 0.004>]
            [0.635, 0.843 color rgbf <0.000, 0.000, 0.000, 0.004>
                          color rgbf <0.545, 0.349, 0.247, 1.000>]
            [0.843, 1.000 color rgbf <0.545, 0.349, 0.247, 1.000>
                          color rgbf <0.545, 0.349, 0.247, 1.000>]
        }
    scale <0.5, 0.5, 1>
    translate <10, 0, 0>
    }
    finish {
        ambient 0.5
        diffuse 0.8
    }
}

//
//   Sandalwood ( makes a great burled maple, too)
//
#declare Sandalwood = 
texture {
    pigment {
        bozo
        turbulence 0.2
        color_map {
            [0.000, 0.171 color rgbf <0.725, 0.659, 0.455, 0.0>
                          color rgbf <0.682, 0.549, 0.420, 0.0>]
            [0.171, 0.274 color rgbf <0.682, 0.549, 0.420, 0.0>
                          color rgbf <0.557, 0.451, 0.322, 0.0>]
            [0.274, 0.393 color rgbf <0.557, 0.451, 0.322, 0.0>
                          color rgbf <0.725, 0.659, 0.455, 0.0>]
            [0.393, 0.564 color rgbf <0.725, 0.659, 0.455, 0.0>
                          color rgbf <0.682, 0.549, 0.420, 0.0>]
            [0.564, 0.701 color rgbf <0.682, 0.549, 0.420, 0.0>
                          color rgbf <0.482, 0.392, 0.278, 0.0>]
            [0.701, 1.000 color rgbf <0.482, 0.392, 0.278, 0.0>
                          color rgbf <0.725, 0.659, 0.455, 0.0>]
        }
    scale <0.2, 0.2, 1>
    scale 2
    }
}
// Sandalwood layer 2
texture {
    pigment {
        bozo
        turbulence 0.8
        color_map {
            [0.000, 0.087 color rgbf <0.682, 0.604, 0.380, 1.000>
                          color rgbf <0.761, 0.694, 0.600, 0.020>]
            [0.087, 0.226 color rgbf <0.761, 0.694, 0.600, 0.020>
                          color rgbf <0.635, 0.553, 0.325, 1.000>]
            [0.226, 0.348 color rgbf <0.635, 0.553, 0.325, 1.000>
                          color rgbf <0.761, 0.694, 0.600, 0.020>]
            [0.348, 0.496 color rgbf <0.761, 0.694, 0.600, 0.020>
                          color rgbf <0.682, 0.604, 0.380, 1.000>]
            [0.496, 0.565 color rgbf <0.682, 0.604, 0.380, 1.000>
                          color rgbf <0.761, 0.694, 0.600, 0.020>]
            [0.565, 0.661 color rgbf <0.761, 0.694, 0.600, 0.020>
                          color rgbf <0.682, 0.604, 0.380, 1.000>]
            [0.661, 0.835 color rgbf <0.682, 0.604, 0.380, 1.000>
                          color rgbf <0.761, 0.694, 0.600, 0.020>]
            [0.835, 1.000 color rgbf <0.761, 0.694, 0.600, 0.020>
                          color rgbf <0.682, 0.604, 0.380, 1.000>]
        }
    scale 0.4
    }
}


//*****************************************************************************
//                           GLASS TEXTURES
//*****************************************************************************

#declare Glass_Finish=
finish {
    specular 1
    roughness 0.001
    ambient 0
    diffuse 0
    reflection 0.1
    refraction 1
    ior 1.5
}

#declare Glass = 
texture {
    pigment { color rgbf<1.0, 1.0, 1.0, 0.7> }
    finish  { Glass_Finish }
}

// Probably more of a "Plexiglas" than glass
#declare Glass2 = 
texture {
    pigment { color Clear }
    finish {
        ambient 0
        diffuse 0
        reflection 0.5
        refraction 0.85
        ior 1.5
        phong 0.3
        phong_size 60
    }
}

// An excellent lead crystal glass!
#declare Glass3 = 
texture {
    pigment { color rgbf <0.98, 0.98, 0.98, 0.9> }
    finish  {
        ambient 0.1
        diffuse 0.1
        reflection 0.1
        refraction 0.9
        ior 1.45
        specular 0.8
        roughness 0.0003
        phong 1 
        phong_size 400
     }
}

#declare Green_Glass = 
texture {
    Glass3
    pigment { color rgbf <0.8, 1, 0.95, 0.9> }
}

// Glass textures contributed by Norm Bowler, of Richland WA 
#declare NBglass =
texture {
    pigment { color rgbf <0.98, 1.0, 0.99, 0.75> }
    finish {
        ambient 0.1
        diffuse 0.1
        reflection .25
        refraction 1
        ior 1.5
        specular 1
        roughness .001
    }
}

#declare NBoldglass=
texture { 
    NBglass
    pigment { color rgbf <0.8, 0.9, 0.85, 0.85> }
}

#declare NBwinebottle=
texture { 
    NBglass
    pigment { color rgbf <0.4, 0.72, 0.4, 0.6> }
}

#declare NBbeerbottle=
texture { 
    NBglass
    pigment { color rgbf <0.7, 0.5, 0.1, 0.6> }
}

// A few color variations on Norm's glass
// Ruby glass
#declare Ruby_Glass =
texture { 
    NBglass
    pigment { color rgbf <0.9, 0.1, 0.2, 0.8> }
}

// Dark, glass
#declare Dark_Green_Glass=
texture { 
    NBglass
    pigment { color rgbf <0.1, 0.7, 0.8, 0.8> }
}

// Yellow glass
#declare Yellow_Glass=
texture { 
    NBglass
    pigment { color rgbf <0.8, 0.8, 0.2, 0.8> }
}

// Orange/Amber glass
#declare Orange_Glass=
texture { 
    NBglass
    pigment { rgbf <1.0, 0.5, 0.0, 0.8> }
}

// Vicks bottle, glass
#declare Vicks_Bottle_Glass=
texture { 
    NBglass
    pigment { color rgbf <0.1, 0.15, 0.5, 0.9> }
}

//*****************************************************************************
//                           METAL FINISHES
//*****************************************************************************

#declare Metal =
finish {
    metallic
    ambient 0.2
    diffuse 0.7
    brilliance 6
    reflection 0.25
    phong 0.75
    phong_size 80
}

//*****************************************************************************
//                           METAL TEXTURES
//*****************************************************************************


// Good looking "metal" textures
// IMPORTANT: They REQUIRE that colors.inc be included BEFORE textures.inc!

#declare Chrome_Texture = 
texture {
    pigment { color LightGray }
    finish {
        ambient 0.3
        diffuse 0.7
        reflection 0.15
        brilliance 8
        specular 0.8
        roughness 0.1
    }
}

// A series of metallic textures using the Metal texture:
#declare Brass_Texture  = texture { pigment{ color Brass      } finish{ Metal }}
#declare Gold_Texture   = texture { pigment{ color BrightGold } finish{ Metal }}
#declare Bronze_Texture = texture { pigment{ color Bronze     } finish{ Metal }}
#declare Copper_Texture = texture { pigment{ color Copper     } finish{ Metal }}
#declare Silver_Texture = texture { pigment{ color Silver     } finish{ Metal }}

// In the future, please refer to Chrome_Texture by this name.  I'd like
// to scrap the old name someday. Ditto with other "_Texture" names!
#declare Chrome_Metal = texture { Chrome_Texture }
#declare Brass_Metal  = texture { Brass_Texture  }
#declare Bronze_Metal = texture { Bronze_Texture }
#declare Gold_Metal   = texture { Gold_Texture   }
#declare Bronze_Metal = texture { Bronze_Texture }
#declare Copper_Metal = texture { Copper_Texture }

// A couple highly reflective metal textures.
#declare Polished_Chrome = 
texture {
    pigment { rgb <0.2, 0.2, 0.2> }
    finish {
        ambient 0.1
        diffuse 0.7
        brilliance 6.0
        reflection 0.6
        phong 0.8
        phong_size 120
    }
}

#declare Polished_Brass = 
texture {
    pigment { color rgb <0.578, 0.422, 0.195> }
    finish {
        metallic
        ambient 0.1
        diffuse 0.8
        brilliance 6.0
        reflection 0.4
        phong 0.8
        phong_size 120
   }
}


// Beautiful military brass texture!
declare New_Brass = 
texture {
    pigment { color rgb <0.70, 0.56, 0.37> }
    finish {
        ambient 0.35
        diffuse 1.0
        brilliance 15
        phong 0.41
        phong_size 5
    }
}


// Spun Brass texture for cymbals & such
#declare Spun_Brass = 
texture { 
    New_Brass
    normal { waves 0.35 frequency 2 scale 0.01 }
}

// Brushed aluminum (brushed along X axis)
#declare Brushed_Aluminum = 
texture {
    Chrome_Metal
    normal {
        bumps -0.5
        scale <1, 0.001, 0.001>
    }
}


#declare SilverFinish =
finish {
    metallic
    ambient 0.25
    diffuse 0.65
    reflection 0.45
    brilliance 6
    phong 1
    phong_size 100
}


// Each of these looks good.  Slightly,r as you go down
#declare Silver1_Colour = color  rgb <0.94, 0.93, 0.83>
#declare Silver2_Colour = color  rgb <0.94, 0.93, 0.86>
#declare Silver3_Colour = color  rgb <0.94, 0.93, 0.90>

#declare Silver1 = 
texture {
    pigment { Silver1_Colour }
    finish { SilverFinish }
}

#declare Silver2 = 
texture {
    pigment { Silver2_Colour }
    finish { SilverFinish }
}

#declare Silver3 = 
texture {
    pigment { Silver3_Colour }
    finish { SilverFinish }
}


// Interesting texture -- Give it a try.
// Sort of a "rgb <0.0, 0.0, 0.0> Hills Gold", black, white, and orange specks or splotches.
#declare Brass_Valley = 
texture {
    pigment {
        granite
        color_map {
            [0.0, 0.3 color rgb <0.82, 0.57, 0.46>  
                      color rgb <0.82, 0.57, 0.46>]
            [0.3, 0.6 color rgb <0.0, 0.0, 0.0>
                      color rgb <0.85, 0.85, 0.95>]
            [0.6, 1.0 color rgb <0.82, 0.57, 0.46>  
                      color rgb <0.85, 0.85, 0.95>]
        }
    }
    finish {
        metallic
        brilliance 6.0
        reflection 0.75
        phong 0.75
    }
}

#declare Rust =
texture { 
    pigment {
        granite
        color_map {
            [0.0, 0.4 color rgb <0.89, 0.51, 0.28>
                      color rgb <0.70, 0.13, 0.00>]
            [0.4, 0.5 color rgb <0.70, 0.13, 0.00>
                      color rgb <0.69, 0.41, 0.08>]
            [0.5, 0.6 color rgb <0.69, 0.41, 0.08>
                      color rgb <0.49, 0.31, 0.28>]
            [0.6, 1.0 color rgb <0.49, 0.31, 0.28>
                      color rgb <0.89, 0.51, 0.28>]
        }
    }
    finish { ambient 0.2 diffuse 0.4 }
}

#declare Rusty_Iron = 
texture {
    pigment {
        granite
        color_map {
            [0.0, 0.5 color rgb <0.21, 0.10, 0.10> * 2
                      color rgb <0.25, 0.25, 0.01> * 2]
            [0.5, 0.6 color rgb <0.25, 0.25, 0.01> * 2
                      color rgb <0.30, 0.10, 0.10> * 2]
            [0.6, 1.0 color rgb <0.15, 0.10, 0.10> * 2
                      color rgb <0.15, 0.10, 0.10> * 2]
        }
    }
    finish { ambient 0.2 diffuse 0.6 }
    normal { wrinkles 1 scale 0.1 }
}


#declare Soft_Silver = 
texture {
    pigment { Silver1_Colour }
    finish {
        metallic
        ambient 0.2
        diffuse 0.35
        specular 0.85
        roughness 0.01
        reflection 0.45
        brilliance 1.5
    }
}

#declare New_Penny = 
texture {
    pigment { color rgb <0.6, 0.45, 0.4> }
    finish {
        metallic
        ambient 0.1
        diffuse 0.65
        specular 0.85
        roughness 0.01
        reflection 0.45
        brilliance 1.5
    }
}

#declare Tinny_Brass = 
texture {
    pigment { color rgb <0.70, 0.56, 0.37> }
    finish {
        metallic
        ambient 0.1
        diffuse 0.65
        specular 0.85
        roughness 0.01
        reflection 0.45
        brilliance 1.5
    }
}
#declare Gold_Nugget = 
texture {
    pigment { color rgb <0.5, 0.35, 0.25> }
    finish {
        metallic
        ambient 0.1
        diffuse 0.65
        specular 0.85
        roughness 0.01
        reflection 0.45
        brilliance 1.5
    }
}
#declare Aluminum  = 
texture {
    pigment { color rgb <0.55, 0.5, 0.45> }
    finish {
        metallic
        ambient 0.1
        diffuse 0.65
        specular 0.85
        roughness 0.01
        reflection 0.45
        brilliance 1.5
    }
}
#declare Bright_Bronze  = 
texture {
    pigment { color rgb <0.36, 0.28, 0.20> }
    finish {
        metallic
        ambient 0.1
        diffuse 0.65
        specular 0.85
        roughness 0.01
        reflection 0.45
        brilliance 1.5
    }
}


//*****************************************************************************
//                    SPECIAL EFFECTS
//*****************************************************************************

// Red & white stripes - Looks best on a y axis Cylinder
// It "spirals" because it's gradient on two axis
#declare Candy_Cane = 
pigment {
    gradient x+y
    color_map {
        [0.00, 0.25 color Red   color Red]
        [0.25, 0.75 color White color White]
        [0.75, 1.00 color Red   color Red]
    }
}

// Orange and Clear stripes spiral around the texture
// to make an object look like it was "Peeled"
// Now, you too can be M.C. Escher 
#declare Peel = 
texture {
    pigment {
        gradient <1, 1, 0 > 
        color_map {
            [0.00, 0.25 color Orange color Orange]
            [0.25, 0.75 color Clear  color Clear]
            [0.75, 1.00 color Orange color Orange]
        }
    }
}

#declare Y_Gradient = 
pigment {
    gradient y
    color_map {
        [0.00, 0.33 color Red   color Blue]
        [0.33, 0.66 color Blue  color Green]
        [0.66, 1.00 color Green color Red]
    }
}

#declare X_Gradient = 
pigment {
    gradient x
    color_map {
        [0.00, 0.33 color Red   color Blue]
        [0.33, 0.66 color Blue  color White]
        [0.66, 1.00 color White color White]
    }
}

// Wavy water 
// Requires a sub-plane, and may require scaling to fit your scene.
#declare Water =
texture {
    pigment{ color rgbf <0.0, 0.0, 1.0, 0.9> }
    normal {
        ripples 0.75
        frequency 10
    }
    finish {
        reflection 0.3
        refraction 0.5
        ior 1.33
    }
}

#declare Cork =
texture {
    pigment {
        granite
        color_map {
            [0.00, 0.60 color rgb <0.93, 0.71, 0.532>
                        color rgb <0.98, 0.81, 0.60>]
            [0.60, 0.65 color rgb <0.50, 0.30, 0.20>
                        color rgb <0.50, 0.30, 0.20>]
            [0.65, 1.00 color rgb <0.80, 0.53, 0.46>
                        color rgb <0.85, 0.75, 0.35>]
        }
    }
    finish{ 
        specular 0.1 
        roughness 0.5 
    }
    scale 0.25     // Generally looks best scaled longer on one axis
}

#declare Lightening_CMap1 =  
color_map {
    [0.00 0.15 color White
               color Very_Light_Purple filter 0.65]
    [0.15 0.25 color Very_Light_Purple filter 0.65 
               color Very_Light_Purple filter 0.65]
    [0.25 0.30 color Very_Light_Purple filter 0.65
               color Light_Purple      filter 0.85]
    [0.30 0.40 color Light_Purple      filter 0.85 
               color Light_Purple      filter 0.85]
    [0.40 0.45 color Light_Purple      filter 0.85 
               color Med_Purple        filter 0.95]
    [0.50 1.00 color Med_Purple        filter 0.95 
               color Med_Purple        filter 0.95]
}

#declare Lightening1 = 
texture {
    pigment {
        marble
        color_map { Lightening_CMap1 }
        turbulence 0.5
    }
    finish { ambient 1 }
}


#declare Lightening_CMap2 =  
color_map {
    [0.00 0.10 color White 
               color Very_Light_Purple filter 0.65]
    [0.10 0.20 color Very_Light_Purple filter 0.65 
               color Very_Light_Purple filter 0.65]
    [0.20 0.30 color Very_Light_Purple filter 0.65  
               color Light_Purple      filter 0.85]
    [0.30 0.45 color Light_Purple      filter 0.85  
               color Light_Purple      filter 0.85]
    [0.45 0.65 color Light_Purple      filter 0.85  
               color Med_Purple        filter 0.95]
    [0.65 1.00 color Med_Purple        filter 0.95
               color Med_Purple        filter 0.95]
}

#declare Lightening2 = 
texture {
    pigment {
        granite
        color_map { Lightening_CMap2 }
        turbulence 0.5
    }
    finish { ambient 1 }
}

// Starfield, by Jeff Burton
#declare Starfield = 
texture {
    pigment {
        granite  
        color_map {
            [ 0.00  0.72 color Black  color Black ]  // No Stars in this area
            [ 0.72  0.75 color Gray20 color Gray40 ] // Very Very Faint Stars
            [ 0.75  0.78 color Gray40 color Gray60 ] // Very Faint Stars
            [ 0.78  0.81 color Gray60 color Gray80 ] // Faint Stars
            [ 0.81  0.85 color Gray80 color Gray95 ] // Medium White Stars
            [ 0.85  0.91 color Gray95 color White  ] // White Stars 
            [ 0.91  1.00 color Black  color Black ]  // No Stars in this area
        }
    scale .015
    }
    finish { ambient 1 }
}

#version Textures_Inc_Temp 
// end of file
